# Simulate io errors when encoding and decoding

Import("env")

env.SkipTestIfFlag('PB_NO_MALLOC')
env.SkipTestIfFlag('PB_NO_STREAM_CALLBACK')

c = Copy("$TARGET", "$SOURCE")
env.Command("alltypes.proto", "#alltypes/alltypes.proto", c)
env.Command("io_errors.c", "#io_errors/io_errors.c", c)

env.NanopbProto(["alltypes", "alltypes.options"])

ioerr = env.Program(["io_errors.c", "alltypes.pb.c",
                     env['NANOPB_OBJS']])

# Run tests under valgrind if available
kwargs = {}
if env.get("VALGRIND"):
    kwargs['COMMAND'] = env['VALGRIND']
    kwargs['ARGS'] = ["-q", "--error-exitcode=99", ioerr[0].abspath]

env.RunTest("io_errors.output", [ioerr, "$BUILD/alltypes/encode_alltypes.output"], **kwargs)


