# Build the common files needed by multiple test cases

Import('env')

malloc_objs = []
if 'PB_NO_MALLOC' not in env.get('CPPDEFINES', {}).keys():
    # Use wrappers for detecting malloc bugs
    env.Append(CPPDEFINES = {'PB_EXTRA_HEADER_NAME': 'malloc_wrappers.h'})
    malloc = env.Object("malloc_wrappers.o", "malloc_wrappers.c")
    env.Depends("$NANOPB/pb.h", ["malloc_wrappers.h"])
    malloc_objs = [malloc]

# Protocol definitions for the encode/decode_unittests
env.NanopbProto("unittestproto")

# Protocol definitions for basic_buffer/stream tests
env.NanopbProto("person")

#--------------------------------------------
# Binaries of the pb_decode.c and pb_encode.c
# These are built using more strict warning flags.
strict = env.Clone()
strict.Append(CFLAGS = strict['CORECFLAGS'])
dec = strict.Object("pb_decode.o", "$NANOPB/pb_decode.c")
enc = strict.Object("pb_encode.o", "$NANOPB/pb_encode.c")
common = strict.Object("pb_common.o", "$NANOPB/pb_common.c")

env.Replace(NANOPB_OBJS = [dec, enc, common] + malloc_objs)
env.Replace(NANOPB_DECODER_OBJS = [dec, common] + malloc_objs)
env.Replace(NANOPB_ENCODER_OBJS = [enc, common] + malloc_objs)
