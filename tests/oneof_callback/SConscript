# Test decoder callback support inside oneofs.

Import('env')

env.SkipTestIfFlag('PB_NO_STRUCT_FIELD_CALLBACK')

env.NanopbProto('oneof')

enc = env.Program(['encode_oneof.c',
                'oneof.pb.c',
                env['NANOPB_ENCODER_OBJS'],
                ])

dec = env.Program(['decode_oneof.c',
                'oneof.pb.c',
                env['NANOPB_DECODER_OBJS'],
                ])

for i in range(1,7):
    # Encode message, then decode with protoc and test program and compare.
    e = env.RunTest("message%d.pb" % i, enc, ARGS = [str(i)])
    d1 = env.Decode([e, "oneof.proto"], MESSAGE = "OneOfMessage")
    d2 = env.RunTest("message%d.txt" % i, [dec, e])
    env.Compare([d1, d2])
