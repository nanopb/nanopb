Import("env")

# Encode cyclic messages with callback fields

c = Copy("$TARGET", "$SOURCE")
env.Command("cyclic_callback.proto", "cyclic.proto", c)
env.NanopbProto(["cyclic_callback", "cyclic_callback.options"])

enc_callback = env.Program(["encode_cyclic_callback.c", "cyclic_callback.pb.c", "$COMMON/pb_encode.o", "$COMMON/pb_common.o"])
dec_callback = env.Program(["decode_cyclic_callback.c", "cyclic_callback.pb.c", "$COMMON/pb_decode.o", "$COMMON/pb_common.o"])

# Test encoding using callbacks
env.RunTest('encode_cyclic_callback.output', [enc_callback, 'testmessage.txt'])
env.Decode('encode_cyclic_callback.output.decoded', ['encode_cyclic_callback.output', 'cyclic.proto'], MESSAGE='Dictionary')
env.Encode('expected.pb', ['expected.txt', 'cyclic.proto'], MESSAGE='Dictionary')
env.Compare(['encode_cyclic_callback.output', 'expected.pb'])

# Test decoding using callbacks
env.RunTest('decode_cyclic_callback.output', [dec_callback, 'encode_cyclic_callback.output'])
env.RunTest('decode_cyclic_callback.output.pb', [enc_callback, 'decode_cyclic_callback.output'])
env.Compare(['decode_cyclic_callback.output.pb', 'encode_cyclic_callback.output'])
